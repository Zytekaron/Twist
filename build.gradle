plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

group = 'com.zytekaron.minecraft.twist'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.1-R0.1-SNAPSHOT'

    implementation 'dev.jorel.CommandAPI:commandapi-core:6.5.3'
    implementation 'dev.jorel.CommandAPI:commandapi-shade:6.5.3'
    compileOnly 'dev.jorel.CommandAPI:commandapi-annotations:6.5.3'
    annotationProcessor 'dev.jorel.CommandAPI:commandapi-annotations:6.5.3'
}

shadowJar {
    dependencies {
        include dependency("dev.jorel.CommandAPI:commandapi-shade:6.5.3")
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
